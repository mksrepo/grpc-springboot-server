
plugins {
	id 'java'
	id 'idea'
	id 'com.google.protobuf' version '0.8.10'
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group 'com.github.simplesteph.grpc'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.17.0"
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.24.0'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation group: 'org.lognet', name: 'grpc-spring-boot-starter', version: '2.3.2'
}

// if you have source imports issues, add the below
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')
idea {
	module {
		// Marks the already(!) added srcDir as "generated"
		generatedSourceDirs += file('build/generated/source')
	}
}

configurations.all {
	resolutionStrategy.eachDependency { details ->
		if ("io.grpc".equalsIgnoreCase(details.requested.group)) {
			details.useVersion "1.37.0"
		}
	}
}

